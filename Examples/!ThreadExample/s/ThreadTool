; ToDo:
; Change the memory model to separate code and data to be able to remove the relative adr addressing
; Modify the handle returned so that is an offset from the start of data memory so if relocation
; takes place the handles still make sense
; Add getter and setter SWI's for various fields
; Add SWI's to Inc and Dec the delay of the Toolbox_RaiseToolboxEvents
; ADD code and SWI's to do OS_CallAfter
; RE-WRITE THE MODULE IN C!


; SWIs
; XOS_Module                 EQU 0x02001E
XOS_CallEvery              EQU 0x02003C
XOS_RemoveTickerEvent      EQU 0x02003D
XToolbox_RaiseToolboxEvent EQU 0x064ECD

; Service calls
Service_WimpCloseDown      EQU 0x53

 AREA |Module$$code|, CODE, READONLY

; Processor Flags and Modes
Negative_flag    * 1 :SHL: 31
Zero_flag        * 1 :SHL: 30
Carry_flag       * 1 :SHL: 29
Overflow_flag    * 1 :SHL: 28
IRQ_flag         * 1 :SHL: 27
FIQ_flag         * 1 :SHL: 26
User_mode        * 0
FIQ_mode         * 1
IRQ_mode         * 2
SVC_mode         * 3

; Module constants
SWI_Block             EQU         0x5AB40
ErrorBlock            EQU         0x822900

; ThreadData offsets
ThreadData_ObjectId              EQU  0
ThreadData_ComponentId           EQU  4
ThreadData_EventValue            EQU  8
ThreadData_Delay                 EQU  12
ThreadData_DelayCount            EQU  16
ThreadData_TaskId                EQU  20

ThreadData_block_size            EQU  24

ThreadData_num_blocks            EQU  10

; ToolboxEventHeader offsets
ToolboxEventHeader_size             EQU  0
ToolboxEventHeader_reference_number EQU  4
ToolboxEventHeader_event_code       EQU  8
ToolboxEventHeader_flags            EQU  12

ToolboxEventHeader_block_size       EQU  16

DelayPeriod EQU 2

; Register names
swi_number            RN          11
pw                    RN          12

; Code
ENTRY
Module_BaseAddr       DCD         0                                   ; Start code
                      DCD         init_code          -Module_BaseAddr ; Initialization code
                      DCD         final_code         -Module_BaseAddr ; Finalization code
                      DCD         service_call       -Module_BaseAddr ; Service call handler
;                      DCD         0
                      DCD         title_string       -Module_BaseAddr ; Title string
                      DCD         help_string        -Module_BaseAddr ; Help string
                      DCD         0                                   ; Help and Command keyword table
                      DCD         SWI_Block                           ; SWI chunk base number
                      DCD         SWI_handler        -Module_BaseAddr ; SWI handler code
                      DCD         SWI_decoding_table -Module_BaseAddr ; SWI decoding table
                      DCD         0                                   ; SWI decoding code
                      DCD         0
                      DCD         module_flags       -Module_BaseAddr ; Module flags offset

title_string          DCB         "ThreadTool",0
help_string           DCB         "ThreadTool \t0.01 (27 Jan 2026) © Bill Antonia",0
                      ALIGN

module_flags          DCB         1
                      ALIGN

init_code             stmfd       sp!,{a1-v3,lr}
; ************************ Initialisation code here **************************
                      mov         a1,#16
                      adr         a2,ToolboxEventHeader
                      str         a1,[a2,#ToolboxEventHeader_size]
                      mov         a1,#0
                      str         a1,[a2,#ToolboxEventHeader_flags]
                      adr         a2,ThreadData
                      mov         a3,#0
init_ThreadDataLoop   mov         a1,#-1
                      str         a1,[a2,#ThreadData_ObjectId]
                      mov         a1,#0
                      str         a1,[a2,#ThreadData_ComponentId]
                      str         a1,[a2,#ThreadData_EventValue]
                      str         a1,[a2,#ThreadData_Delay]
                      str         a1,[a2,#ThreadData_DelayCount]
                      str         a1,[a2,#ThreadData_TaskId]
                      add         a2,a2,#ThreadData_block_size
                      add         a3,a3,#1
                      cmp         a3,#ThreadData_num_blocks
                      bne         init_ThreadDataLoop
                      mov         a1,#DelayPeriod
                      adr         a2,CallEveryHandler
                      mov         a3,#0
                      swi         XOS_CallEvery
                      ldmfd       sp!,{a1-v3,lr}
__exit_normal         teq         r0,r0
                      teq         pc,pc
                      bne         __init_exit_26
                      mov         pc,lr
__init_exit_26        movs        pc,lr

final_code            stmfd       sp!,{a1-a4,v1,lr}
; *********************** Finalisation code here *****************************
                      adr         a1,CallEveryHandler
                      mov         a2,#0
                      swi         XOS_RemoveTickerEvent
                      ldmfd       sp!,{a1-a4,v1,pc}

CallEveryHandler      stmfd       sp!,{a1-a4,v1-v4,lr}
                      mov         v1,#0
                      adr         v2,ThreadData
call_handler_loop     ldr         a2,[v2,#ThreadData_ObjectId]
                      cmp         a2,#-1
                      bne         raise_event
raise_complete        add         v1,v1,#1
                      cmp         v1,#ThreadData_num_blocks
                      beq         exit_call_handler
                      add         v2,v2,#ThreadData_block_size
                      b           call_handler_loop
exit_call_handler     ldmfd       sp!,{a1-a4,v1-v4,lr}
                      mov         pc,lr

raise_event           ldr         a2,[v2,#ThreadData_DelayCount]
                      cmp         a2,#0
                      beq         continue_raise
                      sub         a2,a2,#1
                      cmp         a2,#0
                      strne       a2,[v2,#ThreadData_DelayCount]
                      bne         raise_complete
continue_raise        ldr         a2,[v2,#ThreadData_Delay]
                      str         a2,[v2,#ThreadData_DelayCount]
                      adr         a4,ToolboxEventHeader
                      mov         a1,#ToolboxEventHeader_block_size
                      str         a1,[a4,#ToolboxEventHeader_size]
                      ldr         a3,[v2,#ThreadData_EventValue]
                      str         a3,[a4,#ToolboxEventHeader_event_code]
                      mov         a1,#0
                      str         a1,[a4,#ToolboxEventHeader_reference_number]
                      str         a1,[a4,#ToolboxEventHeader_flags]
                      ldr         a3,[v2,#ThreadData_ComponentId]
                      swi         XToolbox_RaiseToolboxEvent
                      b           raise_complete

; ****************** Stuff to do here *******************************
service_call          cmp         a2,#Service_WimpCloseDown
                      beq         __do_service
                      b           __exit_normal
__do_service          stmfd       sp!,{a1-v1,lr}
                      adr         a2,ThreadData
                      mov         a1,#0
wimpCloseDown_Loop    ldr         a4,[a2,#ThreadData_TaskId]
                      cmp         a3,a4
                      beq         stop_thread
stop_thread_return    add         a1,a1,#1
                      add         a2,a2,#ThreadData_block_size
                      cmp         a1,#ThreadData_num_blocks
                      bne         wimpCloseDown_Loop
                      ldmfd       sp!,{a1-v1,lr}
                      b           __exit_normal

stop_thread           mov         v1,#-1
                      str         v1,[a2,#ThreadData_ObjectId]
                      mov         v1,#0
                      str         v1,[a2,#ThreadData_ComponentId]
                      str         v1,[a2,#ThreadData_EventValue]
                      str         v1,[a2,#ThreadData_Delay ]
                      str         v1,[a2,#ThreadData_DelayCount]
                      str         v1,[a2,#ThreadData_TaskId]
                      b           stop_thread_return


; ****************** Stuff to do here *******************************

SWI_handler           ldr         pw,[pw]
                      cmp         swi_number,#(endofjumptable-jumptable)/4
                      addcc       pc,pc,swi_number,lsl #2
                      b           unknownSWIerror
jumptable             b           threadTool_startThread
                      b           threadTool_stopThread
                      b           threadTool_stopAll
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved
                      b           reserved

endofjumptable

reserved              mov         pc, lr

unknownSWIerror       adr         a1,errmessg
                      b           __exit_error

__exit_error          teq         r0,r0
                      teq         pc,pc
                      bne         __exit_26
                      msr         cpsr_f,#Overflow_flag
                      mov         pc,lr
__exit_26             orr         lr,lr,#Overflow_flag
                      movs        pc,lr

errmessg              DCD         ErrorBlock+0
                      DCB         "Unknown ThreadTool SWI",0
                      ALIGN

SWI_decoding_table    DCB         "ThreadTool",0
                      DCB         "StartThread",0
                      DCB         "StopThread",0
                      DCB         "StopAll",0,0
                      ALIGN

threadTool_startThread stmfd      sp!,{a2-v4,lr}
                      adr         v3,ActiveCount
                      ldr         v4,[v3]
                      cmp         v4,#ThreadData_num_blocks
                      beq         skipActivate
                      add         v4,v4,#1
                      str         v4,[v3]
                      adr         v3,ThreadData
                      mov         v2,#0
assign_loop           ldr         v4,[v3]
                      cmp         v4,#-1
                      beq         assign_block
                      add         v3,v3,#ThreadData_block_size
                      add         v2,v2,#1
                      cmp         v2,#ThreadData_block_size
                      beq         activated
                      b           assign_loop

assign_block          str         a1,[v3,#ThreadData_ObjectId]
                      str         a2,[v3,#ThreadData_ComponentId]
                      str         a3,[v3,#ThreadData_EventValue]
                      str         a4,[v3,#ThreadData_Delay]
                      str         a4,[v3,#ThreadData_DelayCount]
                      str         v1,[v3,#ThreadData_TaskId]
                      mov         a1,v3
activated             ldmfd       sp!,{a2-v4,lr}
                      b           __exit_normal

skipActivate          ldmfd      sp!,{a2-v4,lr}
                      adr         a1,errmessg2
                      b           __exit_error

errmessg2             DCD         ErrorBlock+1
                      DCB         "ThreadBlock not allocated",0
                      ALIGN

threadTool_stopAll   stmfd       sp!,{a1-a4,lr}
                      adr         a1,ThreadData
                      mov         a2,#0
                      mov         a3,#-1
                      mov         a4,#0
stop_all_loop         str         a3,[a1,#ThreadData_ObjectId]
                      str         a4,[a1,#ThreadData_ComponentId]
                      str         a4,[a1,#ThreadData_EventValue]
                      str         a4,[a1,#ThreadData_Delay]
                      str         a4,[a1,#ThreadData_DelayCount]
                      str         a4,[a1,#ThreadData_TaskId]
                      add         a2,a2,#1
                      add         a1,a1,#ThreadData_block_size
                      cmp         a2,#ThreadData_num_blocks
                      bne         stop_all_loop
                      ldmfd       sp!,{a1-a4,lr}

threadTool_stopThread stmfd       sp!,{a2-a4,lr}
                      mov         a2,#-1
                      mov         a4,#0
                      str         a2,[a1,#ThreadData_ObjectId]
                      str         a4,[a1,#ThreadData_ComponentId]
                      str         a4,[a1,#ThreadData_EventValue]
                      str         a4,[a1,#ThreadData_Delay]
                      str         a4,[a1,#ThreadData_DelayCount]
                      str         a4,[a1,#ThreadData_TaskId]
                      adr         a2,ActiveCount
                      ldr         a3,[a2]
                      cmp         a3,#0
                      beq         skip_decrement
                      sub         a3,a3,#1
                      str         a3,[a2]
skip_decrement        ldmfd       sp!,{a2-a4,lr}
                      b           __exit_normal


                      


 AREA |Module$$data|, DATA

ToolboxEventHeader    FILL ToolboxEventHeader_block_size, 0, 4

ThreadData            FILL ThreadData_block_size*ThreadData_num_blocks, 0, 4

ActiveCount           DCD         0

     

                      END












